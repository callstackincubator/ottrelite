import groovy.json.JsonSlurper

import java.nio.file.Files

def opensslVersion = "3.5.3"

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.9.2"
    }
}

def reactNativeArchitectures() {
    def value = rootProject.getProperties().get("reactNativeArchitectures")
    return value ? value.split(",") : ["armeabi-v7a", "x86", "x86_64", "arm64-v8a"]
}

def isNewArchitectureEnabled() {
    return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: 'org.jetbrains.kotlin.android'
apply from: new File(project(':react-native-nitro-modules').projectDir, "fix-prefab.gradle") // fix exported target being INTERFACE not SHARED, including the .so
apply from: '../nitrogen/generated/android/ReactNativeOttreliteInteropOTEL+autolinking.gradle'

if (isNewArchitectureEnabled()) {
    apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["ReactNativeOttreliteInteropOTEL_" + name]
}

def getExtOrIntegerDefault(name) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["ReactNativeOttreliteInteropOTEL_" + name]).toInteger()
}

final def packageJson = new JsonSlurper().parseText(project.file("../package.json").text)
final def packageName = packageJson["name"]
final def packageVersion = packageJson["version"]
final def logPrefix = "[${packageName}] "

def logLifecycle = { msg -> logger.lifecycle("${logPrefix}${msg}") }
def logInfo = { msg -> logger.info("${logPrefix}${msg}") }
def logWarn = { msg -> logger.warn("${logPrefix}${msg}") }
def logError = { msg -> logger.error("${logPrefix}${msg}") }

logLifecycle("ðŸ“ƒ Ottrelite Interop OTEL v${packageVersion} ðŸ“ƒ")

android {
    namespace "com.callstack.ottrelite.interop.otel"

    ndkVersion getExtOrDefault("ndkVersion")
    compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

    defaultConfig {
        minSdkVersion getExtOrIntegerDefault("minSdkVersion")
        targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
        buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()

        externalNativeBuild {
            cmake {
                cppFlags "-frtti -fexceptions -Wall -Wextra -fstack-protector-all"

                def enableDebugLogs = rootProject.hasProperty('OTTRELITE_DEBUG_LOGS') ? rootProject.property('OTTRELITE_DEBUG_LOGS').toBoolean() : false
                arguments "-DANDROID_STL=c++_shared", "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON", "-DOTTRELITE_INTEROP_OTEL_VERSION=${packageVersion}", "-DOTTRELITE_DEBUG_LOGS=${enableDebugLogs}"
                abiFilters(*reactNativeArchitectures())

                buildTypes {
                    debug {
                        cppFlags "-O1 -g"
                    }
                    release {
                        cppFlags "-O2"
                    }
                }
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    packagingOptions {
        excludes = [
                "META-INF",
                "META-INF/**",
                "**/libc++_shared.so",
                "**/libfbjni.so",
                "**/libjsi.so",
                "**/libfolly_json.so",
                "**/libfolly_runtime.so",
                "**/libglog.so",
                "**/libhermes.so",
                "**/libhermes-executor-debug.so",
                "**/libhermes_executor.so",
                "**/libreactnative.so",
                "**/libreactnativejni.so",
                "**/libturbomodulejsijni.so",
                "**/libreact_nativemodule_core.so",
                "**/libjscexecutor.so"
        ]
    }

    buildFeatures {
        buildConfig true
        prefab true
        prefabPublishing true
    }

    prefab {
        ReactNativeOttreliteInteropOTEL {
            headers "${project.buildDir}/headers/"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        disable "GradleCompatible"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            if (isNewArchitectureEnabled()) {
                java.srcDirs += [
                        // React Codegen files
                        "${project.buildDir}/generated/source/codegen/java"
                ]
            }
        }
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    // For < 0.71, this will be from the local maven repo
    // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"

    // Add a dependency on NitroModules
    implementation project(":react-native-nitro-modules")

    // Depend on Ottrelite core
    implementation project(":ottrelite_core")
}

def isReleaseBuild = project.hasProperty("buildType") && project.buildType == "release"
def cxxBuildVariantDirName = isReleaseBuild ? "Release" : "Debug"
def thirdPartySourceDir = file("${project.projectDir}/../third_party").absolutePath
def otelSourceDir = "$thirdPartySourceDir/opentelemetry-cpp"
def openSSLForAndroidSourceDir = "$thirdPartySourceDir/openssl_for_android"
def brotliSourceDir = "$thirdPartySourceDir/brotli"
def zlibSourceDir = "$thirdPartySourceDir/zlib"
def curlSourceDir = "$thirdPartySourceDir/curl"
def wrapperRootDir = "$thirdPartySourceDir/wrapper"
def getWrapperBuildDir = { String abi -> "$wrapperRootDir/android/$cxxBuildVariantDirName/$abi" }
def getGeneric3PBuildDir = { String libName, String abi -> "$wrapperRootDir/android/$cxxBuildVariantDirName/$abi/$libName" }
def getOpenSSLForAndroidBuildDir = { String abi -> new File(openSSLForAndroidSourceDir, "openssl_${opensslVersion}_${abi}").path }
def getBrotliBuildDir = { String abi -> getGeneric3PBuildDir("brotli", abi) }
def getZlibBuildDir = { String abi -> getGeneric3PBuildDir("zlib", abi) }
def getCurlBuildDir = { String abi -> getGeneric3PBuildDir("curl", abi) }
def getCmakeInstallDir = { String abi -> "$wrapperRootDir/android/install/$abi" }

def prepareOttreliteInteropHeadersTask = tasks.register('prepareOttreliteInteropHeadersTask', Copy) {
    from files(
            fileTree('../cpp/include/public/'),
            fileTree('../nitrogen/generated/shared/c++/')
    ).filter { it.isFile() }
    // flatten the structure of directories
    include "*.hpp"
    into "${project.buildDir}/headers/ReactNativeOttreliteInteropOTEL"
    includeEmptyDirs = false
}

preBuild.dependsOn(prepareOttreliteInteropHeadersTask)

tasks.register("buildOpenSSL") {
    def openSSLSourceDirFile = new File(openSSLForAndroidSourceDir, "openssl-${opensslVersion}")
    def openSSLSourceDir = openSSLSourceDirFile.path

    def opensslTarName = "openssl-${opensslVersion}.tar.gz"
    def opensslUrl = "https://github.com/openssl/openssl/releases/download/openssl-${opensslVersion}/${opensslTarName}"

    doFirst {
        def tarFile = file("${temporaryDir}/${opensslTarName}")

        def openSSLForAndroidSourceDirFile = file(openSSLForAndroidSourceDir)
        if (!openSSLForAndroidSourceDirFile.exists()) {
            println("[@ottrelite/interop-otel] Cloning openssl_for_android to ${openSSLForAndroidSourceDir}...")

            if (!openSSLForAndroidSourceDirFile.exists()) {
                openSSLForAndroidSourceDirFile.mkdirs()

                exec {
                    commandLine "git", "clone", "--depth", "1", "https://github.com/217heidai/openssl_for_android.git", openSSLForAndroidSourceDir
                }
            } else {
                println("[@ottrelite/interop-otel] openssl_for_android already cloned in ${openSSLForAndroidSourceDir}")
            }
        }

        if (!openSSLSourceDirFile.exists()) {
            openSSLSourceDirFile.mkdirs()

            println("[@ottrelite/interop-otel] Downloading OpenSSL ${opensslVersion} to ${tarFile.path}...")
            new URL(opensslUrl).withInputStream { i ->
                tarFile.withOutputStream { it << i }
            }

            println("[@ottrelite/interop-otel] Extracting OpenSSL from ${tarFile.path} to ${openSSLSourceDir}...")

            copy {
                from tarTree(tarFile.path)

                // below: actually, the script appends the openssl-${opensslVersion} directory by itself
                into openSSLSourceDirFile.parent
            }
        } else {
            println("[@ottrelite/interop-otel] OpenSSL already extracted to ${openSSLSourceDir}")
        }

        def symlinkTarget = new File(openSSLSourceDirFile.parent, "android-ndk-${android.ndkVersion}")
        if (!symlinkTarget.exists()) {
            println("[@ottrelite/interop-otel] Symlinking the Android NDK for openssl_for_android to ${openSSLSourceDirFile.parent}...")
            Files.createSymbolicLink(symlinkTarget.toPath(), android.ndkDirectory.toPath())
        } else {
            println("[@ottrelite/interop-otel] Symlink target ${symlinkTarget} already exists.")
        }
    }

    doLast {
        reactNativeArchitectures().each { abi ->
            if (new File(getCmakeInstallDir(abi), "lib/libssl.a").exists()) {
                println("[@ottrelite/interop-otel] OpenSSL already built, skipping build.")
            } else {
                exec {
                    println("[@ottrelite/interop-otel] Building OpenSSL using openssl_for_android for ABI $abi from ${openSSLForAndroidSourceDir} and writing to source dir...")

                    commandLine "bash", new File(openSSLForAndroidSourceDir, "openssl_build_new.sh").path,
                            "${android.defaultConfig.minSdkVersion.apiLevel}",
                            "$abi",
                            "$opensslVersion",
                            "$android.ndkVersion"
                }
            }
        }
    }
}

tasks.register("copyOpenSSL") {
    doLast {
        reactNativeArchitectures().each { abi ->
            def openSSLForAndroidBuildDir = getOpenSSLForAndroidBuildDir(abi)
            def cmakeInstallDir = getCmakeInstallDir(abi)

            println("[@ottrelite/interop-otel] Copying OpenSSL build artifacts for ABI $abi from ${openSSLForAndroidBuildDir} to ${cmakeInstallDir}...")

            copy {
                from fileTree(openSSLForAndroidBuildDir) {
                    include "include/**"
                    include "lib/**"
                }

                into cmakeInstallDir
            }
        }
    }
}

copyOpenSSL.mustRunAfter(buildOpenSSL)
copyOpenSSL.dependsOn(buildOpenSSL)

tasks.register("buildAndInstallBrotli") {
    doFirst {
        if (!new File(brotliSourceDir, "CMakeLists.txt").exists()) {
            println("[@ottrelite/interop-otel] Cloning Brotli to ${brotliSourceDir}...")

            if (!file(brotliSourceDir).exists()) {
                file(brotliSourceDir).mkdirs()
                exec {
                    commandLine "git", "clone", "--branch", "v1.1.0", "https://github.com/google/brotli.git", brotliSourceDir
                }
            }
        } else {
            println("[@ottrelite/interop-otel] Brotli already cloned in ${brotliSourceDir}")
        }
    }

    doLast {
        reactNativeArchitectures().each { abi ->
            if (new File(getCmakeInstallDir(abi), "lib/libbrotlienc.a").exists()) {
                println("[@ottrelite/interop-otel] Brotli already built and installed to ${getCmakeInstallDir(abi)}, skipping build.")
            } else {
                def brotliBuildDir = getBrotliBuildDir(abi)
                def cmakeInstallDir = getCmakeInstallDir(abi)

                println("[@ottrelite/interop-otel] Building & installing Brotli artifacts for ABI $abi, this may take a while...")

                if (!new File(brotliBuildDir).exists()) {
                    file(brotliBuildDir).mkdirs()
                }

                exec {
                    println("[@ottrelite/interop-otel] Configuring Brotli for ABI $abi from ${brotliSourceDir} and writing to ${brotliBuildDir}...")

                    commandLine "cmake", "-S", brotliSourceDir, "-B", brotliBuildDir,
                            "-DANDROID_ABI=$abi",
                            "-DANDROID_PLATFORM=${android.defaultConfig.minSdkVersion.apiLevel}",
                            "-DCMAKE_TOOLCHAIN_FILE=${android.ndkDirectory}/build/cmake/android.toolchain.cmake",
                            "-DCMAKE_BUILD_TYPE=${cxxBuildVariantDirName}",
                            "-DCMAKE_CXX_FLAGS_DEBUG=-g",
                            "-DBROTLI_BUILD_TOOLS=OFF",
                            "-DBUILD_SHARED_LIBS=OFF"
                }

                println("[@ottrelite/interop-otel] Building for ABI $abi ${brotliBuildDir}...")

                exec {
                    commandLine "cmake", "--build", brotliBuildDir, "-j8"
                }

                println("[@ottrelite/interop-otel] Installing build artifacts for ABI $abi to ${cmakeInstallDir}...")

                exec {
                    commandLine "cmake", "--install", brotliBuildDir, "--prefix", cmakeInstallDir
                }

                println("[@ottrelite/interop-otel] Brotli build & install finished for ABI $abi")
            }
        }
    }
}

tasks.register("buildAndInstallZlib") {
    doFirst {
        if (!new File(zlibSourceDir, "CMakeLists.txt").exists()) {
            println("[@ottrelite/interop-otel] Cloning zlib to ${zlibSourceDir}...")

            if (!file(zlibSourceDir).exists()) {
                file(zlibSourceDir).mkdirs()
                exec {
                    commandLine "git", "clone", "--depth", "1", "--branch", "v1.3.1", "https://github.com/madler/zlib.git", zlibSourceDir
                }
            }
        } else {
            println("[@ottrelite/interop-otel] zlib already cloned in ${zlibSourceDir}")
        }
    }

    doLast {
        reactNativeArchitectures().each { abi ->
            def cmakeInstallDir = getCmakeInstallDir(abi)

            if (new File(cmakeInstallDir, "lib/libz.a").exists()) {
                println("[@ottrelite/interop-otel] zlib already built and installed to ${getCmakeInstallDir(abi)}, skipping build.")
            } else {
                def zlibBuildDir = getZlibBuildDir(abi)
                println("[@ottrelite/interop-otel] Building & installing zlib artifacts for ABI $abi, this may take a while...")

                if (!new File(zlibBuildDir).exists()) {
                    file(zlibBuildDir).mkdirs()
                }

                exec {
                    println("[@ottrelite/interop-otel] Configuring zlib for ABI $abi from ${zlibSourceDir} and writing to ${zlibBuildDir}...")

                    commandLine "cmake", "-S", zlibSourceDir, "-B", zlibBuildDir,
                            "-DANDROID_ABI=$abi",
                            "-DANDROID_PLATFORM=${android.defaultConfig.minSdkVersion.apiLevel}",
                            "-DCMAKE_TOOLCHAIN_FILE=${android.ndkDirectory}/build/cmake/android.toolchain.cmake",
                            "-DCMAKE_BUILD_TYPE=${cxxBuildVariantDirName}",
                            "-DCMAKE_CXX_FLAGS_DEBUG=-g",
                            "-DZLIB_BUILD_TESTING=OFF",
                            "-DZLIB_BUILD_SHARED=OFF",
                            "-DZLIB_BUILD_STATIC=ON",
                            "-DZLIB_BUILD_MINIZIP=OFF",
                            "-DZLIB_INSTALL=ON",
                            "-DZLIB_BUILD_EXAMPLES=OFF",
                            "-DCMAKE_INSTALL_PREFIX=${cmakeInstallDir}"
                }

                println("[@ottrelite/interop-otel] Building for ABI $abi ${zlibBuildDir}...")

                exec {
                    commandLine "cmake", "--build", zlibBuildDir, "--config", isReleaseBuild ? "Release" : "Debug", "-j8"
                }

                println("[@ottrelite/interop-otel] Installing build artifacts for ABI $abi to ${cmakeInstallDir}...")

                exec {
                    commandLine "cmake", "--install", zlibBuildDir, "--prefix", cmakeInstallDir
                }

                println("[@ottrelite/interop-otel] zlib build & install finished for ABI $abi")
            }
        }
    }
}

tasks.register("buildAndInstallCURL") {
    doFirst {
        if (!new File(curlSourceDir, "CMakeLists.txt").exists()) {
            println("[@ottrelite/interop-otel] Cloning CURL to ${curlSourceDir}...")

            if (!file(curlSourceDir).exists()) {
                file(curlSourceDir).mkdirs()
                exec {
                    commandLine "git", "clone", "--depth", "1", "--branch", "curl-8_15_0", "https://github.com/curl/curl.git", curlSourceDir
                }
            }
        } else {
            println("[@ottrelite/interop-otel] CURL already cloned in ${curlSourceDir}")
        }
    }

    doLast {
        reactNativeArchitectures().each { abi ->
            def cmakeInstallDir = getCmakeInstallDir(abi)

            if (new File(cmakeInstallDir, "lib/libcurl${isReleaseBuild ? "" : "-d"}.a").exists()) {
                println("[@ottrelite/interop-otel] CURL already built and installed to ${getCmakeInstallDir(abi)}, skipping build.")
            } else {
                def curlBuildDir = getCurlBuildDir(abi)
                println("[@ottrelite/interop-otel] Building & installing CURL artifacts for ABI $abi, this may take a while...")

                if (!new File(curlBuildDir).exists()) {
                    file(curlBuildDir).mkdirs()
                }

                exec {
                    println("[@ottrelite/interop-otel] Configuring CURL for ABI $abi from ${curlSourceDir} and writing to ${curlBuildDir}...")

                    commandLine "cmake", "-S", curlSourceDir, "-B", curlBuildDir,
                            "-DANDROID_ABI=$abi",
                            "-DANDROID_PLATFORM=${android.defaultConfig.minSdkVersion.apiLevel}",
                            "-DCMAKE_TOOLCHAIN_FILE=${android.ndkDirectory}/build/cmake/android.toolchain.cmake",
                            "-DCMAKE_BUILD_TYPE=${cxxBuildVariantDirName}",
                            "-DCMAKE_CXX_FLAGS_DEBUG=-g",
                            "-DZLIB_ROOT=\"$cmakeInstallDir\"",
                            "-DOPENSSL_ROOT_DIR=${cmakeInstallDir}",
                            "-DOPENSSL_SSL_LIBRARY=${cmakeInstallDir}/lib/libssl.a",
                            "-DOPENSSL_CRYPTO_LIBRARY=${cmakeInstallDir}/lib/libcrypto.a",
                            "-DOPENSSL_INCLUDE_DIR=${cmakeInstallDir}/include",
                            "-DUSE_NGHTTP2=OFF",
                            "-DCURL_ZSTD=OFF",
                            "-DBUILD_SHARED_LIBS=OFF",
                            "-DCMAKE_USE_OPENSSL=ON",
                            "-DBUILD_CURL_EXE=OFF",
                            "-DBUILD_TESTING=OFF",
                            "-DUSE_LIBIDN2=OFF",
                            "-DCURL_CA_PATH=none",
                            "-DCURL_DISABLE_FTP=ON",
                            "-DCURL_DISABLE_LDAP=ON",
                            "-DCURL_DISABLE_LDAPS=ON",
                            "-DCURL_DISABLE_TELNET=ON",
                            "-DCURL_DISABLE_DICT=ON",
                            "-DCURL_DISABLE_FILE=ON",
                            "-DCURL_DISABLE_TFTP=ON",
                            "-DCURL_DISABLE_RTSP=ON",
                            "-DCURL_DISABLE_POP3=ON",
                            "-DCURL_DISABLE_IMAP=ON",
                            "-DCURL_DISABLE_SMTP=ON",
                            "-DCURL_DISABLE_GOPHER=ON",
                            "-DBUILD_EXAMPLES=OFF",
                            "-DENABLE_CURL_MANUAL=OFF",
                            "-DCURL_USE_LIBPSL=OFF"
                }

                println("[@ottrelite/interop-otel] Building for ABI $abi ${curlBuildDir}...")

                exec {
                    commandLine "cmake", "--build", curlBuildDir, "--config", isReleaseBuild ? "Release" : "Debug", "-j8"
                }

                println("[@ottrelite/interop-otel] Installing build artifacts for ABI $abi to ${cmakeInstallDir}...")

                exec {
                    commandLine "cmake", "--install", curlBuildDir, "--prefix", cmakeInstallDir
                }

                println("[@ottrelite/interop-otel] CURL build & install finished for ABI $abi")
            }
        }
    }
}

buildAndInstallCURL.dependsOn(copyOpenSSL)

// CPP wrapper + OTEL CPP SDK
tasks.register("buildAndInstallCppWrapper") {
    doFirst {
        if (!new File(otelSourceDir, "CMakeLists.txt").exists()) {
            println("[@ottrelite/interop-otel] Cloning OpenTelemetry C++ SDK to ${otelSourceDir}...")

            if (!file(otelSourceDir).exists()) {
                file(otelSourceDir).mkdirs()
                exec {
                    commandLine "git", "clone", "--depth", "1", "--branch", "v1.23.0", "https://github.com/open-telemetry/opentelemetry-cpp.git", otelSourceDir
                }
            }
        } else {
            println("[@ottrelite/interop-otel] OpenTelemetry C++ SDK already cloned in ${otelSourceDir}")
        }
    }

    doLast {
        reactNativeArchitectures().each { abi ->
            def wrapperBuildDir = getWrapperBuildDir(abi)
            def cmakeInstallDir = getCmakeInstallDir(abi)

            println("[@ottrelite/interop-otel] Building & installing artifacts for ABI $abi, this may take a while...")

            if (!new File(wrapperBuildDir).exists()) {
                file(wrapperBuildDir).mkdirs()
            }

            exec {
                println("[@ottrelite/interop-otel] Configuring for ABI $abi from ${thirdPartySourceDir} and writing to ${wrapperBuildDir}...")

                commandLine "cmake", "-S", thirdPartySourceDir, "-B", wrapperBuildDir,
                        "-DANDROID_ABI=$abi",
                        "-DANDROID_PLATFORM=${android.defaultConfig.minSdkVersion.apiLevel}",
                        "-DCMAKE_TOOLCHAIN_FILE=${android.ndkDirectory}/build/cmake/android.toolchain.cmake",
                        "-DCMAKE_BUILD_TYPE=${cxxBuildVariantDirName}",
                        "-DCMAKE_CXX_FLAGS_DEBUG=-g",
                        "-Dabsl_DIR=$cmakeInstallDir/lib/cmake/absl",
                        "-Dutf8_range_DIR=$cmakeInstallDir/lib/cmake/utf8_range",
                        "-DCURL_DIR=$cmakeInstallDir/lib/cmake/CURL",
                        "-DOPENSSL_ROOT_DIR=${cmakeInstallDir}",
                        "-DOPENSSL_SSL_LIBRARY=${cmakeInstallDir}/lib/libssl.a",
                        "-DOPENSSL_CRYPTO_LIBRARY=${cmakeInstallDir}/lib/libcrypto.a",
                        "-DOPENSSL_INCLUDE_DIR=${cmakeInstallDir}/include"
            }

            println("[@ottrelite/interop-otel] Building for ABI $abi ${wrapperBuildDir}...")

            exec {
                commandLine "cmake", "--build", wrapperBuildDir, "-j8"
            }

            println("[@ottrelite/interop-otel] Installing build artifacts for ABI $abi to ${cmakeInstallDir}...")

            exec {
                commandLine "cmake", "--install", wrapperBuildDir, "--prefix", cmakeInstallDir
            }

            println("[@ottrelite/interop-otel] Build & install finished for ABI $abi")
        }
    }
}

buildAndInstallCppWrapper.dependsOn(copyOpenSSL)
buildAndInstallCppWrapper.dependsOn(buildAndInstallBrotli)
buildAndInstallCppWrapper.dependsOn(buildAndInstallZlib)
buildAndInstallCppWrapper.dependsOn(buildAndInstallCURL)

preBuild.dependsOn(buildAndInstallCppWrapper)
